group 'com.tiza'
version '1.0'

apply plugin: 'java'

sourceCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile fileTree(dir: project(':tstar-plugin').projectDir.path + "/libs/tstar-op", include: "**/*.jar")

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.7'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.36'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// 设置编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    archiveName = 'sih_heatmap.jar'
    manifest {
        attributes(
                'Main-Class': 'com.tiza.tstar.op.sih_heatmap.Main'
        )
    }
}

// 拷贝文件(排除项目自带jar)
task copyJar(type: Copy) {
    from configurations.runtime
    into "$buildDir/libs/lib"

    exclude {
        !it.file.name.contains('tstar-plugin') && it.file.path.contains('libs\\tstar')
    }
}

// 拷贝配置文件
task copyConf(type: Copy) {
    from "$projectDir/src/main/conf"
    into "$buildDir/libs/conf"
}

// 打包
task zip(type: Zip, dependsOn: [copyConf, copyJar, jar]) {
    archiveName = 'sih_heatmap.zip'
    from 'build/libs'
}


